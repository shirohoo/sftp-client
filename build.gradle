plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group = 'io.github.shirohoo'
archivesBaseName = 'sftp-client'
version = '1.7'
sourceCompatibility = JavaVersion.VERSION_1_8

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

configurations {
    extraLibs
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    extraLibs fileTree(include: ['*.jar'], dir: 'libs')
    configurations.compile.extendsFrom(configurations.extraLibs)

    implementation (
            'org.slf4j:slf4j-api:1.7.25'
    )

    testImplementation (
            'org.junit.jupiter:junit-jupiter:5.6.0',
            'org.assertj:assertj-core:3.15.0'
    )
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'sftp-client'
                packaging 'jar'
                description 'It helps to make handling CRUD operations via SFTP protocol very simple. This module is written based on JSch.'
                url 'https://github.com/shirohoo/sftp-client'

                scm {
                    connection 'https://github.com/shirohoo/sftp-client'
                    developerConnection 'https://shirohoo.github.io/'
                    url 'https://github.com/shirohoo/sftp-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'shirohoo'
                        name 'Changhun Han'
                        email 'shirohoo4102@gmail.com'
                    }
                }
            }
        }
    }
}
